buildscript {
    repositories {
        maven {
            url("http://openbakery.org/repository/")
        }
        mavenCentral()
    }
    dependencies {
        classpath group: "org.openbakery", name: "xcodePlugin", version: "0.9.15"
    }
}

apply plugin: "xcode"

xcodebuild {
    scheme        = PROJECT_NAME
    workspace     = PROJECT_NAME + ".xcworkspace"
    infoPlist     = PROJECT_NAME + "/Info.plist"
    configuration = "Debug"
}

infoplist {
    version = BUILD_NUMBER
}

task debug(dependsOn: ["build", "test"]) {
    description = "Performs a build for the simulator and runs all unit test in the for the iPhone and iPad simulators"
}

task release(dependsOn: "archive") {
    description = "Performs a build for the device signed using the default certificate"
}

gradle.taskGraph.whenReady { taskGraph ->

    if (taskGraph.hasTask(test)) {
        println "Configure build settings and test for simulator"
        xcodebuild {
            destination {
                platform = "iOS Simulator"
                name     = "iPad 2"
                os       = "8.*"
            }
            destination {
                platform = "iOS Simulator"
                name     = "iPhone 4s"
                os       = "8.*"
            }
        }
    }

    if (taskGraph.hasTask(archive)) {
        println "Configure build settings for release"
        xcodebuild {
            configuration = "Release"
            sdk           = "iphoneos"
        }
    }

}
